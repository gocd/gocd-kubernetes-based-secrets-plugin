/*
 * Copyright 2022 Thoughtworks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'java'
}
apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"

gocdPlugin {
    id = 'cd.go.contrib.secrets.kubernetes'
    pluginVersion = '2.0.1'
    goCdVersion = '22.1.0'
    name = 'Kubernetes Secrets Plugin for GoCD'
    description = 'Kubernetes secrets based secrets plugin for GoCD'
    vendorName = 'Thoughtworks, Inc.'
    vendorUrl = 'https://github.com/gocd/gocd-kubernetes-based-secrets-plugin'

    githubRepo {
        owner = System.getenv('GITHUB_USER') ?: 'bob'
        repo = 'gocd-kubernetes-based-secrets-plugin'
        token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
    }

    pluginProject = project

    prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
    prereleaseDryrun = !"Yes".equalsIgnoreCase(System.getenv('PRERELEASE'))
    assetsToRelease = [project.tasks.findByName('jar')]
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile).configureEach {
    options.release = 17
}

version = gocdPlugin.fullVersion(project)

repositories {
    mavenCentral()
}

static String versionOf(String dependencySpec) {
    return dependencySpec.split(':').last()
}

ext {
    deps = [
            gocdPluginApi      : 'cd.go.plugin:go-plugin-api:25.4.0',
            kubernetesClientApi: 'io.fabric8:kubernetes-client-api:7.4.0',
    ]

    versions = project.ext.deps.collectEntries { lib, libGav -> [lib, versionOf(libGav)] }
}

dependencies {
    compileOnly project.deps.gocdPluginApi
    implementation 'cd.go.plugin.base:gocd-plugin-base:1.1.1'
    implementation project.deps.kubernetesClientApi
    constraints {
        implementation 'io.vertx:vertx-web-client:4.5.23' // Upgrade transitive version within fabric kubernetes-client
    }
    runtimeOnly "io.fabric8:kubernetes-client:${project.versions.kubernetesClientApi}"
    implementation platform('org.slf4j:slf4j-bom:2.0.17')
    runtimeOnly 'org.slf4j:slf4j-simple'

    testImplementation project.deps.gocdPluginApi
    testImplementation platform('org.junit:junit-bom:6.0.1')
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.assertj:assertj-core:3.27.7'
    testImplementation 'org.mockito:mockito-core:5.21.0'
    testImplementation 'org.skyscreamer:jsonassert:1.5.3'
    testImplementation 'org.jsoup:jsoup:1.22.1'
}

test {
    useJUnitPlatform()
}

jar {
    from(configurations.runtimeClasspath) {
        into "lib/"
    }
}

