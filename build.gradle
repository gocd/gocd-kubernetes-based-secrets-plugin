/*
 * Copyright 2022 Thoughtworks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"

gocdPlugin {
    id = 'cd.go.contrib.secrets.kubernetes'
    pluginVersion = '1.2.1'
    goCdVersion = '20.9.0'
    name = 'Kubernetes Secrets Plugin for GoCD'
    description = 'Kubernetes secrets based secrets plugin for GoCD'
    vendorName = 'Thoughtworks, Inc.'
    vendorUrl = 'https://github.com/gocd/gocd-kubernetes-based-secrets-plugin'

    githubRepo {
        owner = System.getenv('GITHUB_USER') ?: 'bob'
        repo = 'gocd-kubernetes-based-secrets-plugin'
        token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
    }

    pluginProject = project

    prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
    assetsToRelease = [project.tasks.findByName('jar')]
}

sourceCompatibility = 1.11
targetCompatibility = 1.11

version = gocdPlugin.fullVersion(project)

repositories {
    mavenCentral()
}

static String versionOf(String dependencySpec) {
    return dependencySpec.split(':').last()
}

ext {
    deps = [
            gocdPluginApi      : 'cd.go.plugin:go-plugin-api:22.2.0',
            junit              : 'org.junit.jupiter:junit-jupiter-api:5.9.0',
            kubernetesClientApi: 'io.fabric8:kubernetes-client-api:6.1.1',
    ]

    versions = project.ext.deps.collectEntries { lib, libGav -> [lib, versionOf(libGav)] }
}

dependencies {
    implementation group: 'cd.go.plugin.base', name: 'gocd-plugin-base', version: '0.0.4'
    compileOnly project.deps.gocdPluginApi
    implementation project.deps.kubernetesClientApi
    runtimeOnly group: 'io.fabric8', name: 'kubernetes-client', version: project.versions.kubernetesClientApi

    testImplementation project.deps.junit
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: project.versions.junit
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: project.versions.junit
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.23.1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.8.0'
    testImplementation group: 'org.jsoup', name: 'jsoup', version: '1.15.3'
    testImplementation group: 'cd.go.plugin', name: 'go-plugin-api', version: project.versions.gocdPluginApi
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.1'
}

test {
    useJUnitPlatform()
}

jar {
    from(configurations.runtimeClasspath) {
        into "lib/"
    }
}

